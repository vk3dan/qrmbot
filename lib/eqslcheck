#!/usr/bin/perl -w
# Lookup callsign on eQSL.  2-clause BSD license.

# Copyright 2018 /u/molo1134. All rights reserved.

use strict;
use utf8;
use feature 'unicode_strings';
binmode(STDOUT, ":utf8");

use File::Basename;
use Cwd 'realpath';
use lib dirname(realpath(__FILE__));
use Colors;

my $username = $ENV{'USER'} || $ENV{'USERNAME'} || getpwuid($<);

# configuration
my $exitnonzeroonerror = 1;
$exitnonzeroonerror = 0 if $username eq 'eggdrop';

my $baseurl = "http://www.eqsl.cc/Member.cfm?";

if ($#ARGV < 0 || length($ARGV[0]) == 0) {
  if ($username eq 'eggdrop') {
    print "usage: !eqsl <callsign>\n";
  } else {
    print "usage: $0 <callsign>\n";
  }
  exit $exitnonzeroonerror;
}

# when calling from eggdrop, parameters may be packed into first parameter.
if ($#ARGV == 0 && $ARGV[0] =~ m/ /) {
  @ARGV = split / /, $ARGV[0];
}

my $call = uc $ARGV[0];
$call =~ s/[^A-Z0-9\/]//g;

my $found = undef;
my $ag = 0;
my $inloggedin = 0;
my $lastlogin = undef;
my $grid = undef;
my $member = undef;

#print "$baseurl$call\n";

open(HTTP, '-|', "curl -4 -k -L --max-time 4 --retry 1 -s '$baseurl$call'");
while (<HTTP>) {
  #print;
  if (/service.*unavailable/) {
    print "Service unavailable.\n";
    exit $exitnonzeroonerror;
  }
  if (/is not registered yet/) {
    $found = 0;
    last;
  }
  if (/Effective dates at this QTH/) {
    $found = 1;
  }
  if (/not registered any more/) {
    $found = 2;
    last;
  }
  if (/AuthGuaranteed.gif/) {
    $ag = 1;
  }
  if (/I logged in/) {
    $inloggedin = 1;
  }
  if ($inloggedin == 1 and /\s*(on.*?|.*?ago)\s*$/) {
    $lastlogin = $1;
    $lastlogin =~ s/on\s*//;
    $inloggedin = 0;
  }
  if (/Grid Square ([A-R]{2}[0-9]{2}([a-z]{2})?)/i) {
    $grid = $1;
    $grid =~ s/^([A-R]{2})/uc($1)/ie;
    $grid =~ s/([a-z]{2})$/lc($1)/ie;
  }
  if (/BronzeMember.gif/) {
    # example: DJ2HB
    $member = "bronze";
  }
  if (/SilverMember.gif/) {
    # exmaple: HL4CCM
    $member = "Ag";
  }
  if (/GoldMember.gif/) {
    # yeah baby
    # example: IZ2XAH
    $member = "Au";
  }
  if (/PlatinumMember.gif/) {
    # example: SA0BUX
    $member = "Pt";
  }
}
close(HTTP);

if (!defined($found)) {
  print "No result.  Timeout?\n";
  exit $exitnonzeroonerror;
}

if ($found == 0) {
  print bold($call), ": not registered with eQSL\n";
  exit 0;
} elsif ($found == 2) {
  print bold($call), ": no longer registered with eQSL\n";
  exit 0;
}

print bold($call), ": eQSL ";
print bold("(AG)"), " " if $ag == 1;
print yellow("✪") . " "	    if defined($member) and $member eq "Au";
print lightGrey("✪") . " "  if defined($member) and $member eq "Ag";
print grey("✪") . " "	    if defined($member) and $member eq "Pt";
print darkYellow("✪") . " " if defined($member) and $member eq "bronze";
print "$grid" if defined $grid;
print "; last login: $lastlogin" if defined $lastlogin;
print "\n";

exit 0;

