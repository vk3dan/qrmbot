#!/usr/bin/perl -w

# substantially hustled from the btc code written by:
# 2-clause BSD license.
# Copyright 2018 /u/molo1134. All rights reserved.
#
# Requires an API key from AlphaVantage. Place into your
# $HOME/.alphavantage as follows:
#
# $apikey = "1234567890ABCDEF";
#
# NB: some things are in here, I assume, to make this
# script play well with eggdrop and other IRC-related
# stuff.

use strict;
use warnings;
use utf8;
use feature 'unicode_strings';
binmode(STDOUT, ":utf8");

use Math::Round;
use URI::Escape;

use File::Basename;
use Cwd 'realpath';
use lib dirname(realpath(__FILE__));
use Colors;
use Util;

my $username = $ENV{'USER'} || $ENV{'USERNAME'} || getpwuid($<);

@ARGV=split(' ', join(' ',@ARGV));

our $apikey = undef;

# change this to 0 to always exit with success
our $exitnonzeroonerror = 1;
$exitnonzeroonerror = 0 if $username eq 'eggdrop';

my $quote;
if ($#ARGV < 0 || length($ARGV[0]) == 0) {
  $quote = "DJIA";
} else {
  $quote = $ARGV[0];
}

my $apikeyfile = $ENV{'HOME'} . "/.alphavantage";
if (-e ($apikeyfile)) {
  require($apikeyfile);
} else {
  die "error: unable to read file $apikeyfile"
}
die "error: no API key set" unless defined($apikey);

# The AlphaVantage API outputs the following fields for point-in-time quotes:
#
# CSV:
#
# symbol,open,high,low,price,volume,latestDay,previousClose,change,changePercent
#
# JSON:
#
# {
#    "Global Quote": {
#        "01. symbol": "^DJI",
#        "02. open": "24736.5400",
#        "03. high": "24975.6800",
#        "04. low": "24645.5600",
#        "05. price": "24958.2000",
#        "06. volume": "159230140",
#        "07. latest trading day": "2018-10-25",
#        "08. previous close": "24583.4200",
#        "09. change": "374.7793",
#        "10. change percent": "1.5245%"
#    }
#
# Select via the datatype key/value pair in the URL (cvs or json)
#
# see https://www.alphavantage.co/documentation/ for more info.

my $url = "https://www.alphavantage.co/query?function=GLOBAL_QUOTE&symbol=$quote&apikey=$apikey&datatype=csv";

my ($ticker, $open, $price, $vol, $chg, $pct);

open(HTTP, '-|', "curl --max-time 10 -s -k -L '$url'");
binmode(HTTP, ":utf8");
while (<HTTP>) {
	chomp; chomp; # // CRLF
	if (/if you would like to target a higher API call frequency./) {
		print "rate exceeded. slow down.\n";
		exit $exitnonzeroonerror;
	}
	s/\r//g;
	next if $. < 2; # this skips the first line of CSV headers

	($ticker, $open, undef, undef, $price, $vol, undef, undef, $chg, $pct) = split(',', $_);
}
close(HTTP);

if (not defined($ticker) or not defined($pct)) {
	my $encquote = uri_escape($quote);
	my $url = "https://www.alphavantage.co/query?function=SYMBOL_SEARCH&keywords=$encquote&apikey=$apikey&datatype=csv";
	my $found = 0;

	open(HTTP, '-|', "curl --max-time 10 -s -k -L '$url'");
	binmode(HTTP, ":utf8");
	while (<HTTP>) {
		next if m/^symbol,/;
		if (/if you would like to target a higher API call frequency./) {
			print "rate exceeded. slow down.\n";
			exit $exitnonzeroonerror;
		}
		my ($symb,$name,$type,$reg) = split(',');
		# TODO: switch to JSON API for symbol search
		if ($symb eq "{}") {
		  print "not found\n";
		  exit $exitnonzeroonerror;
		}
		print(bold($symb), ": $name - $type, $reg; ");
		$found = 1;
	}
	print "\n" if $found;

	if (not $found) {
		print "no data returned: $quote\n";
	}
	exit $exitnonzeroonerror;
}

my $trend = 1;
$trend = 0 if defined($pct) and $pct =~ m/-/;

my $trendsymbol;
$trendsymbol = green("↑") if $trend > 0;
$trendsymbol = red("↓") if $trend <= 0;

# the little circles look cool, too
#$trend = green("●");
#$trend = red("●") if $info[9] =~ /-/;

# lop off some trailing 0s
$price =~ s/(\.[0-9]*?[1-9]?)0+$/$1/;
$price =~ s/\.$//;
$open  =~ s/(\.[0-9]*?[1-9]?)0+$/$1/;
$open  =~ s/\.$//;
$chg   =~ s/(\.[0-9]*?[1-9]?)0+$/$1/;
$chg   =~ s/\.$//;

$pct =~ s/%//;
$pct = round($pct * 10) / 10;

$price = commify($price);
$open = commify($open);
$chg = commify($chg);
$vol = commify($vol);

print bold($ticker), " ", "$trendsymbol$price ";

print "+$chg (" if $trend;
print "$chg (" if not $trend;

print green("+$pct") if $trend;
print red($pct) if not $trend;

print "%) open: $open volume: $vol\n";

